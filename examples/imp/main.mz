main() :=
    let env := Cell("a", Int(2i32), Cell("x", Int(7u32), Nil()));
    fetchCell(evalProgram(example(), env), "out");

// Computes "out = a^x" using a simple while-loop.
example() :=
    Cons(Assign("out", Int(1i32)),
    Cons(Assign("i", Var("x")),
    Cons(While(GreaterThan(Var("i"), Int(0u32)),
        Cons(Assign("out", Mul(Var("out"), Var("a"))),
        Cons(Assign("i", Sub(Var("i"), Int(1u32))),
        Nil()))),
    Nil())));

evalProgram(program, env) := match program {
    Nil() -> env,
    Cons(stmt, rest) -> evalProgram(rest, evalStmt(stmt, env))
};

evalStmt(stmt, env) := match stmt {
    Assign(x, t) -> updateCell(env, x, evalTerm(t, env)),
    While(cond, repeat) -> match evalBoolean(cond, env) {
        T() -> evalStmt(stmt, evalProgram(repeat, env)),
        F() -> env
    }
};

evalTerm(t, env) := match t {
    Var(x) -> fetchCell(env, x),
    Boolean(b) -> Boolean(b),
    Int(n) -> Int(n),
    Sub(t1, t2) ->
        let result := -(evalInt(t1, env), evalInt(t2, env));
        Int(result),
    Mul(t1, t2) ->
        let result := *(evalInt(t1, env), evalInt(t2, env));
        Int(result),
    GreaterThan(t1, t2) ->
        let result := >(evalInt(t1, env), evalInt(t2, env));
        Boolean(result)
};

evalBoolean(t, env) := match evalTerm(t, env) {
    Boolean(b) -> b,
    Int(_n) -> Panic("want `Boolean(b)`, got `Int(n)`")
};

evalInt(t, env) := match evalTerm(t, env) {
    Int(n) -> n,
    Boolean(_b) -> Panic("want `Int(n)`, got `Boolean(b)`")
};

fetchCell(env, x) := match env {
    Nil() -> Panic(++("the variable is unbound: ", x)),
    Cell(y, value, xs) -> match =(x, y) {
        T() -> value,
        F() -> fetchCell(xs, x)
    }
};

updateCell(env, x, value) := match env {
    Nil() -> Cell(x, value, Nil()),
    Cell(y, value', xs) -> match =(x, y) {
        T() -> Cell(x, value, xs),
        F() ->
            let xs := updateCell(xs, x, value);
            Cell(y, value', xs)
    }
};
